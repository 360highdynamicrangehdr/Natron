# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=x264
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
replaces=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=20180212
pkgrel=1.1
pkgdesc="Library for encoding H264/AVC video streams (mingw-w64)"
arch=('any')
url="https://www.videolan.org/developers/x264.html"
license=("custom")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config" "yasm")
depends=("${MINGW_PACKAGE_PREFIX}-libwinpthread"
         "${MINGW_PACKAGE_PREFIX}-l-smash")
options=('strip' 'staticlibs')
source=("http://ftp.videolan.org/pub/videolan/x264/snapshots/${_realname}-snapshot-${pkgver}-2245-stable.tar.bz2"
        0001-beautify-pc.all.patch
        0002-install-avisynth_c.h.mingw.patch)
sha256sums=('SKIP'
            '0eba3b41ef0c01f33280ce61bdd47cb966d765c0cb02db239fc6c89d601a2596'
            'e3653ebf07432bbef7ce8bc52352f5547ad9f1ebb35bf9257c09d7ee751a335e')


prepare() {
  cd ${_realname}-snapshot-${pkgver}-2245-stable
  patch -p1 -i ${srcdir}/0001-beautify-pc.all.patch
  patch -p1 -i ${srcdir}/0002-install-avisynth_c.h.mingw.patch
}

build() {
  [[ -d "build-${MINGW_CHOST}" ]] && rm -rf "build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"
  # add --bit-depth=10 to build a x264 that can only produce 10bit videos
  ../${_realname}-snapshot-${pkgver}-2245-stable/configure \
    --prefix=${MINGW_PREFIX} \
    --host=${MINGW_CHOST} \
    --enable-static \
    --enable-shared

    make -j${MKJOBS}
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
